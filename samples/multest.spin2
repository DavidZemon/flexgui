''
'' program to test multiply
''
OBJ
  ser: "SimpleSerial"

PUB main | a,b
  ser.start(115_200)
  ser.str(string("Multiply test...", 13, 10))
  try(1, 1)
  try($7fffffff, $ffffffff)
  try($12345, $ffff)

PUB try(a, b) | t, lo, hi
  (lo,hi,t) := swmul(a, b)
  report(string("software mul"), lo, hi, t)
  (lo,hi,t) := cordicmul(a, b)
  report(string("cordic"), lo, hi, t)
  (lo,hi,t) := hwmul(a, b)
  report(string("mul16"), lo, hi, t)

PUB report(msg, x, y, t)
  ser.str(msg)
  ser.str(string(" gives "))
  ser.hex(x, 8)
  ser.tx(",")
  ser.hex(y, 8)
  ser.str(string( " in "))
  ser.dec(t)
  ser.str(string(" cycles.", 13, 10))

PUB builtin_mul(a, b) : r1, r2, t
  t := CNT
  r1 := a*b
  r2 := a**b
  t := CNT - t

PUB cordicmul(a, b) : lo, hi, t | m1, m2
  t := CNT
  lo := a
  asm
    qmul a, b
    getqx lo
    getqy hi
  endasm
  t := CNT - t

PUB hwmul(a, b) : lo, hi, t | ahi, bhi
  t := CNT
  ahi := a>>16
  bhi := b>>16
  asm
    mov lo, a
    mov hi, ahi
    mul lo, b
    mul hi, bhi
    mul ahi, b
    mul bhi, a
    mov a, ahi
    shl a, #16
    shr ahi, #16
    mov b, bhi
    shl b, #16
    shr bhi, #16
    add lo, a wc
    addx hi, ahi
    add lo, b wc
    addx hi, bhi
  endasm
  t := CNT - t

{
PRI add64(alo, ahi, blo, bhi): rlo, rhi
  rlo := alo
  rhi := ahi
  asm
    add rlo, blo wc
    addx rhi, bhi
  endasm
}
 
PUB swmul(a, b): lo, hi, t | bhi
  t := CNT
  lo := 0
  hi := 0
  bhi := 0
  repeat while (a <> 0)
    if (a&1)
      ''(lo,hi) := add64(lo, hi, b, bhi)
      asm
        add lo, b wc
        addx hi, bhi
      endasm
    '
    '(b, bhi) := add64(b, bhi, b, bhi)
    asm
      add b, b wc
      addx bhi, bhi
    endasm
    a >>= 1
 
  t := CNT - t
